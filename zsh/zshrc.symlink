export DOTFILES="$HOME/.dotfiles"
export PATH="$DOTFILES/bin:$DOTFILES/local/bin:$PATH"

# Store all zsh dotfiles
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# Load all environment variables
for file in ${(M)config_files:#*env.zsh}; do
  source "$file"
done

# Load all other files
for file in ${config_files:#*/(env|completion|action).zsh}; do
  source "$file"
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# Load completion files
for file in ${(M)config_files:#*completion.zsh}; do
  source "$file"
done

# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# Load all actions
for file in ${(M)config_files:#*action.zsh}; do
  source "$file"
done

unset config_files
