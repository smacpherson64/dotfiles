export ZSH="$HOME/.dotfiles"

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# Stash local environment variables in ~/.localrc.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# load all environment variables
for file in ${(M)config_files:#*/env.zsh}
do
  source $file
done

# load everything but the env and completion files
for file in ${${config_files:#*/env.zsh}:#*/completion.zsh:#*/env.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down
